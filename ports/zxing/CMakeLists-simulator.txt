#
# CMake listfile to specify the build process, see:
# http://www.cmake.org/cmake/help/documentation.html
#
project(zxing)
cmake_minimum_required(VERSION 2.8.0)

# Check for polluted source tree.
if(EXISTS ${CMAKE_SOURCE_DIR}/CMakeCache.txt OR
    EXISTS ${CMAKE_SOURCE_DIR}/CMakeFiles)
    message(FATAL_ERROR
        "Source directory is polluted:"
        "\n  * remove CMakeCache.txt"
        "\n  * remove CMakeFiles directory")
endif()

# Suppress in-source builds.
if(${CMAKE_SOURCE_DIR} STREQUAL ${CMAKE_BINARY_DIR})
    message(FATAL_ERROR
        "CMake generation is not allowed within the source directory:"
        "\n  * mkdir build"
        "\n  * cd build"
        "\n  * Unix-like: cmake -G \"Unix Makefiles\" .."
        "\n  * Windows: cmake -G \"Visual Studio 10\" ..")
endif()

# Adjust CMake's module path.
set(CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/cmake/)

# Suppress MSVC CRT warnings.
if(MSVC)
    add_definitions(-D_CRT_SECURE_NO_WARNINGS)
    add_definitions(/Za)
endif()

# Add libzxing library.
file(GLOB_RECURSE LIBZXING_FILES
    "./core/src/*.cpp"
    "./core/src/*.h"
    "./core/src/*.cc"
    "./core/src/*.hh"
)
if(WIN32)
    file(GLOB LIBZXING_WIN32_FILES
        "./core/lib/win32/*.c"
        "./core/lib/win32/*.h"
    )
    set(LIBZXING_FILES ${LIBZXING_FILES} ${LIBZXING_WIN32_FILES})
    include_directories(SYSTEM "./core/lib/win32/")
endif()
include_directories("./core/src/")
add_library(libzxing STATIC ${LIBZXING_FILES})
set_target_properties(libzxing PROPERTIES PREFIX "")
find_package(Iconv)
if(ICONV_FOUND)
    include_directories(${ICONV_INCLUDE_DIR})
    target_link_libraries(libzxing ${ICONV_LIBRARIES})
else()
    add_definitions(-DNO_ICONV=1)
endif()

# Add cli executable.
file(GLOB_RECURSE ZXING_FILES
    "./cli/src/*.cpp"
    "./cli/src/*.h"
)
add_executable(zxing ${ZXING_FILES})
target_link_libraries(zxing libzxing)

# Install headers.
function(zxing_install_headers path)
    file(GLOB headers
        "./core/src/${path}/*.h"
        "./core/src/${path}/*.hh"
    )
    install(FILES ${headers} DESTINATION "include/zxing-2.2/${path}")
endfunction()

zxing_install_headers(bigint)
zxing_install_headers(zxing)
zxing_install_headers(zxing/aztec)
zxing_install_headers(zxing/aztec/decoder)
zxing_install_headers(zxing/aztec/detector)
zxing_install_headers(zxing/common)
zxing_install_headers(zxing/common/detector)
zxing_install_headers(zxing/common/reedsolomon)
zxing_install_headers(zxing/datamatrix)
zxing_install_headers(zxing/datamatrix/decoder)
zxing_install_headers(zxing/datamatrix/detector)
zxing_install_headers(zxing/multi)
zxing_install_headers(zxing/multi/qrcode)
zxing_install_headers(zxing/multi/qrcode/detector)
zxing_install_headers(zxing/oned)
zxing_install_headers(zxing/pdf417)
zxing_install_headers(zxing/pdf417/decoder)
zxing_install_headers(zxing/pdf417/decoder/ec)
zxing_install_headers(zxing/pdf417/detector)
zxing_install_headers(zxing/qrcode)
zxing_install_headers(zxing/qrcode/decoder)
zxing_install_headers(zxing/qrcode/detector)

# Install zxing executable and libzxing library.
install(TARGETS zxing libzxing
    RUNTIME DESTINATION bin
    LIBRARY DESTINATION lib
    ARCHIVE DESTINATION lib
)
